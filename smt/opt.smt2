(define-sort BInt () (_ BitVec 32))
(declare-const c1 BInt)
(declare-const c2 BInt)
(declare-const c3 BInt)
(declare-const c4 BInt)
(declare-const c5 BInt)
(declare-const t BInt)
(declare-const signed Bool)
(define-fun merge-trunc-16 ((small BInt) (big BInt)) BInt
  (bvor (bvand small #x0000FFFF) (bvand big #xFFFF0000)))
(define-fun merge-trunc-low8 ((small BInt) (big BInt)) BInt
  (bvor (bvand small #x000000FF) (bvand big #xFFFFFF00)))
(define-fun merge-trunc-high8 ((small BInt) (big BInt)) BInt
  (bvor (bvshl (bvand small #x000000FF) #x00000008) (bvand big #xFFFF00FF)))
(define-fun zero-extend-16 ((arg BInt)) BInt ((_ zero_extend 16) ((_ extract 15 0) arg)))
(define-fun zero-extend-low8 ((arg BInt)) BInt ((_ zero_extend 24) ((_ extract 7 0) arg)))
(define-fun zero-extend-high8 ((arg BInt)) BInt ((_ zero_extend 24) ((_ extract 15 8) arg)))
(define-fun sign-extend-16 ((arg BInt)) BInt ((_ sign_extend 16) ((_ extract 15 0) arg)))
(define-fun sign-extend-low8 ((arg BInt)) BInt ((_ sign_extend 24) ((_ extract 7 0) arg)))
(define-fun sign-extend-high8 ((arg BInt)) BInt ((_ sign_extend 24) ((_ extract 15 8) arg)))

(define-fun extend-16 ((arg BInt)) BInt (ite signed (sign-extend-16 arg) (zero-extend-16 arg)))
(define-fun extend-low8 ((arg BInt)) BInt (ite signed (sign-extend-low8 arg) (zero-extend-low8 arg)))
(define-fun extend-high8 ((arg BInt)) BInt (ite signed (sign-extend-high8 arg) (zero-extend-high8 arg)))

(declare-const m-op Int)
(define-fun merge ((small BInt) (big BInt)) BInt 
  (ite (= m-op 0 ) (merge-trunc-16 small big)
  (ite (= m-op 1) (merge-trunc-low8 small big) (merge-trunc-high8 small big))))
(define-fun mextend ((arg BInt)) BInt 
  (ite (= m-op 0) (ite signed (sign-extend-16 arg) (zero-extend-16 arg))
  (ite (= m-op 1) (ite signed (sign-extend-low8 arg) (zero-extend-low8 arg))
    (ite signed (sign-extend-high8 arg) (zero-extend-high8 arg)))))

(push)
(assert (ite signed (= t (sign-extend-low8 c1)) (= t (zero-extend-low8 c1))))
(push)
(assert (or (= c1 (merge-trunc-low8 c2 c3)) (= c1 (merge-trunc-16 c2 c3))))
(assert (not (ite signed (= t (sign-extend-low8 c2)) (= t (zero-extend-low8 c2)))))
(check-sat)
(pop)
(push)
(assert (= c1 (merge-trunc-high8 c2 c3)))
(assert (not (ite signed (= t (sign-extend-low8 c3)) (= t (zero-extend-low8 c3)))))
(check-sat)
(pop)
(pop)

(push)
(assert (ite signed (= t (sign-extend-high8 c1)) (= t (zero-extend-high8 c1))))
(push)
(assert (= c1 (merge-trunc-high8 c2 c3)))
(assert (not (ite signed (= t (sign-extend-low8 c2)) (= t (zero-extend-low8 c2)))))
(check-sat)
(pop)
(push)
(assert (= c1 (merge-trunc-16 c2 c3)))
(assert (not (ite signed (= t (sign-extend-high8 c2)) (= t (zero-extend-high8 c2)))))
(check-sat)
(pop)
(push)
(assert (= c1 (merge-trunc-low8 c2 c3)))
(assert (not (ite signed (= t (sign-extend-high8 c3)) (= t (zero-extend-high8 c3)))))
(check-sat)
(pop)
(pop)

(push)
(assert (not (= (merge-trunc-low8 c1 (merge-trunc-low8 c2 c3)) (merge-trunc-low8 c1 c3))))
(check-sat)
(pop)

(push)
(assert (not (= (merge-trunc-high8 c1 (merge-trunc-high8 c2 c3)) (merge-trunc-high8 c1 c3))))
(check-sat)
(pop)

(push)
(assert (or (= t (merge-trunc-16 c2 c3)) (= t (merge-trunc-low8 c2 c3)) (= t (merge-trunc-high8 c2 c3))))
(assert (not (= (merge-trunc-16 c1 (merge-trunc-16 c2 c3)) (merge-trunc-16 c1 c3))))
(check-sat)
(pop)

(push)
(assert (or (= m-op 0) (= m-op 1)))
(assert (not (= (merge (mextend c1) c2) (merge c1 c2))))
(check-sat)
(pop)
(push)
(assert (or (= m-op 1) (= m-op 2)))
(assert (not (= (merge (extend-16 c1) c2) (merge c1 c2))))
(check-sat)
(pop)

(push)
(declare-const op Int)
(define-fun op-rel ((lhs BInt) (rhs BInt)) BInt
	(ite (= op 0) (bvadd lhs rhs) 
	(ite (= op 1) (bvsub lhs rhs) 
	(ite (= op 2) (bvand lhs rhs) 
	(ite (= op 3) (bvor lhs rhs) 
    (ite (= op 4) (bvshl lhs (zero-extend-low8 rhs)) (bvxor lhs rhs)))))))
(assert (or (= m-op 0) (= m-op 1)))
(declare-const m-e-op Bool)
(define-fun m-e ((arg BInt) (big BInt)) BInt
  (ite m-e-op (merge arg big) (mextend arg)))
(push)
(assert (not (= (m-e (op-rel (m-e c1 c3) (m-e c2 c4)) c5) (m-e (op-rel c1  c2) c5))))
(check-sat)
(pop)
(push)
(assert (not (= (m-e (op-rel (merge-trunc-16 c1 c3) (merge-trunc-16 c2 c4)) c5) (m-e (op-rel c1 c2) c5))))
(check-sat)
(pop)
(push)
(assert (not (= (m-e (op-rel (extend-16 c1) (extend-16 c2)) c3) (m-e (op-rel c1 c2) c3))))
(check-sat)
(pop)
(pop)

(push)
(assert (not (= (merge c1 (merge c2 c3)) (merge c1 c3))))
(check-sat)
(pop)
(push)
(assert (or (= m-op 1) (= m-op 2)))
(assert (not (= (merge-trunc-16 c1 (merge c2 c3)) (merge-trunc-16 c1 c3))))
(check-sat)
(pop)

(push)
(assert (or (= m-op 0) (= m-op 1)))
(push)
(assert (not (= (merge (merge c1 c2) c3) (merge c1 c3))))
(check-sat)
(push)
(assert (not (= (merge (mextend c1) c2) (merge c1 c2))))
(check-sat)
(pop)
(pop)

(push)
(assert (not (= ((_ rotate_right 4) c1) (bvor (bvlshr c1 #x00000004) (bvshl c1 (bvsub #x00000020 #x00000004))))))
(check-sat)
(pop)
(push)
(assert (not (= ((_ rotate_left 4) c1) (bvor (bvshl c1 #x00000004) (bvlshr c1 (bvsub #x00000020 #x00000004))))))
(check-sat)
(pop)

(push)
(define-fun mul-add-16bit ((lhs (_ BitVec 128)) (rhs (_ BitVec 128))) (_ BitVec 128) 
  (concat 
          (bvadd (bvmul ((_ zero_extend 16) ((_ extract 111 96) lhs)) ((_ zero_extend 16) ((_ extract 111 96) rhs))) (bvmul ((_ zero_extend 16) ((_ extract 127 112) lhs)) ((_ zero_extend 16) ((_ extract 127 112) rhs))))
          (bvadd (bvmul ((_ zero_extend 16) ((_ extract 79 64) lhs)) ((_ zero_extend 16) ((_ extract 79 64) rhs))) (bvmul ((_ zero_extend 16) ((_ extract 95 80) lhs)) ((_ zero_extend 16) ((_ extract 95 80) rhs))))
          (bvadd (bvmul ((_ zero_extend 16) ((_ extract 47 32) lhs)) ((_ zero_extend 16) ((_ extract 47 32) rhs))) (bvmul ((_ zero_extend 16) ((_ extract 63 48) lhs)) ((_ zero_extend 16) ((_ extract 63 48) rhs))))
          (bvadd (bvmul ((_ zero_extend 16) ((_ extract 15 0 ) lhs)) ((_ zero_extend 16) ((_ extract 15 0) rhs))) (bvmul ((_ zero_extend 16) ((_ extract 31 16) lhs)) ((_ zero_extend 16) ((_ extract 31 16) rhs))))))
(define-fun replace ((low (_ BitVec 128)) (high (_ BitVec 64))) (_ BitVec 128)
  (concat high ((_ extract 63 0) low)))
(declare-const v1 (_ BitVec 128))
(declare-const v2 (_ BitVec 128))
(declare-const u1 (_ BitVec 64))
(declare-const u2 (_ BitVec 64))
(assert (not (= (replace (mul-add-16bit (replace v1 u1) v2) u2) (replace (mul-add-16bit v1 v2) u2))))
(check-sat)
(pop)
