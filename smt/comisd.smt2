(declare-const lhs Float64)
(declare-const rhs Float64)
(declare-const res Bool)

(declare-const pf Bool)
(declare-const cf Bool)
(declare-const zf Bool)
(assert (= pf (or (fp.isNaN lhs) (fp.isNaN rhs))))
(assert (= zf (or pf (fp.eq lhs rhs))))
(assert (= cf (or pf (fp.lt lhs rhs))))

(push)
(assert (not (= pf (or (not (fp.eq lhs lhs)) (not (fp.eq rhs rhs))))))
(echo "JP")
(check-sat)
(get-model)
(pop)

(push)
(assert (not (= (not pf) (and (fp.eq lhs lhs) (fp.eq rhs rhs)))))
(echo "JNP")
(check-sat)
(get-model)
(pop)

(push)
(assert (= res (fp.geq lhs rhs)))
(assert (not (= (not cf) res )))
(echo "JAE")
(check-sat)
(get-model)
(pop)

(push)
(assert (= res (not (fp.geq lhs rhs))))
(assert (not (= cf res)))
(echo "JB")
(check-sat)
(get-model)
(pop)

(push)
(assert (= res (or (fp.gt lhs rhs) (fp.lt lhs rhs))))
(assert (not (= (not zf) res)))
(echo "JNE")
(check-sat)
(get-model)
(pop)
