(declare-const lhs Float64)
(declare-const rhs Float64)
(declare-const res Bool)

(declare-const c2 Bool)
(assert (= c2 (or (fp.isNaN lhs) (fp.isNaN rhs))))

(declare-const c0 Bool)
(assert (= c0 (or c2 (fp.lt lhs rhs))))

(declare-const c3 Bool)
(assert (= c3 (or c2 (fp.eq lhs rhs))))

(define-fun bool->bit ((b Bool)) (_ BitVec 1) (ite b #b1 #b0))
(declare-const ah (_ BitVec 8))
(assert (= ((_ extract 0 0) ah) (bool->bit c0)))
(assert (= ((_ extract 2 2) ah) (bool->bit c2)))
(assert (= ((_ extract 6 6) ah) (bool->bit c3)))

(declare-const c (_ BitVec 8))

(define-fun popcount ((bv (_ BitVec 8))) (_ BitVec 3) 
	(bvadd 
		((_ zero_extend 2) ((_ extract 0 0) bv))
		((_ zero_extend 2) ((_ extract 1 1) bv))
		((_ zero_extend 2) ((_ extract 2 2) bv))
		((_ zero_extend 2) ((_ extract 3 3) bv))
		((_ zero_extend 2) ((_ extract 4 4) bv))
		((_ zero_extend 2) ((_ extract 5 5) bv))
		((_ zero_extend 2) ((_ extract 6 6) bv))
		((_ zero_extend 2) ((_ extract 7 7) bv))
	))
(declare-const pf Bool)
(assert (= pf (= ((_ extract 0 0) (popcount (bvand c ah))) #b0)))

(declare-const zf Bool)
(assert (= zf (= (bvand c ah) #x00)))

(push)
(assert (= c #x41))
(assert (= res (not (fp.leq lhs rhs))))
(assert (not (= res pf)))
(echo "JP, 0x41")
(check-sat)
(pop)

(push)
(assert (= c #x44))
(assert (not (= pf (not (fp.eq lhs rhs)))))
(echo "JP, 0x44")
(check-sat)
(pop)

(push)
(assert (= c #x01))
(assert (not (= (not zf) (not (fp.geq lhs rhs)))))
(echo "JNE, 0x1")
(check-sat)
(pop)

(push)
(assert (= c #x05))
(assert (not (= pf (not (fp.lt lhs rhs)))))
(echo "JP, 0x5")
(check-sat)
(pop)

(push)
(assert (= c #x05))
(assert (not (= (not pf) (fp.lt lhs rhs))))
(echo "JNP, 0x5")
(check-sat)
(pop)

(push)
(assert (= c #x44))
(assert (not (= (not pf) (fp.eq lhs rhs))))
(echo "JNP, 0x44")
(check-sat)
(pop)

(push)
(assert (= c #x41))
(assert (not (= (not zf) (not (fp.gt lhs rhs)))))
(echo "JNE, 0x41")
(check-sat)
(pop)

(push)
(assert (= c #x41))
(assert (not (= pf (not (fp.leq lhs rhs)))))
(echo "JE, 0x41")
(check-sat)
(pop)

(push)
(assert (= c #x41))
(assert (not (= (not pf) (fp.leq lhs rhs))))
(echo "JNP, 0x41")
(check-sat)
(pop)

(push)
(assert (= c #x01))
(assert (not (= zf (fp.geq lhs rhs))))
(echo "JE, 0x1")
(check-sat)
(pop)
